
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗагрузитьНастройкиНаСервере();
	
	ЭтаИнтеграционнаяОбработка = РеквизитФормыВЗначение("Объект");
	
	Заголовок = СтрШаблон("%1 (v.%2)", ЭтаИнтеграционнаяОбработка.СведенияОВнешнейОбработке().Наименование,
		ЭтаИнтеграционнаяОбработка.СведенияОВнешнейОбработке().Версия);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		
		Оповещение = Новый ОписаниеОповещения("ПередЗакрытиемПодтверждение", ЭтотОбъект);
		ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповещение, Отказ, ЗавершениеРаботы);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОповещениеОбУстановкеОбработкиНадписьОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьФормуДополнительныхОбработок" Тогда
		ОткрытьФорму("Справочник.ДополнительныеОтчетыИОбработки.ФормаСписка");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьФормуТестирования(Команда)
	
	ОткрытьФорму("ВнешняяОбработка.SimpleИнтеграцияГИСМТ.Форма.Тестирование");
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройки(Команда)
	
	СохранитьНастройкиНаСервере();
	
	Модифицированность = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СвойствоСтруктуры(Знач Структура, Ключ, ЗначениеПоУмолчанию = Неопределено)
	
	Если СтрНайти(Ключ, ".") > 0 Тогда
		
		КлючВСтруктуре = Лев(Ключ, СтрНайти(Ключ, ".") - 1);
		ОстатокКлюча = Сред(Ключ, СтрНайти(Ключ, ".") + 1);
		
		Результат = СвойствоСтруктуры(Структура, КлючВСтруктуре, ЗначениеПоУмолчанию);
		
		Если Результат = ЗначениеПоУмолчанию Или ПустаяСтрока(ОстатокКлюча) Тогда
			Возврат Результат;
		Иначе
			Возврат СвойствоСтруктуры(Результат, ОстатокКлюча, ЗначениеПоУмолчанию);
		КонецЕсли;
		
	Иначе
		
		Возврат ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Структура, Ключ, ЗначениеПоУмолчанию);
		
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ЗагрузитьНастройкиНаСервере()
	
	ЭтаИнтеграционнаяОбработка = РеквизитФормыВЗначение("Объект");
	
	НастройкиОбработки = ЭтаИнтеграционнаяОбработка.ПолучитьНастройки();
	
	ОтключитьЗапросКСервисуПриЗапросеСоставаКМ = СвойствоСтруктуры(НастройкиОбработки, "ЗапросСоставаКМ.ОтключитьЗапросКСервису", Ложь);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиНаСервере()
	
	ЭтаИнтеграционнаяОбработка = РеквизитФормыВЗначение("Объект");
	
	НовыеНастройки = ЭтаИнтеграционнаяОбработка.НовыеНастройкиЭталон();
	НовыеНастройки.ЗапросСоставаКМ.ОтключитьЗапросКСервису = ОтключитьЗапросКСервисуПриЗапросеСоставаКМ;
	
	ЭтаИнтеграционнаяОбработка.СохранитьНастройки(НовыеНастройки);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемПодтверждение(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	СохранитьНастройкиНаСервере();
	
	Модифицированность = Ложь;
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти
